plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    namespace 'eu.devhuba.anigafi'
    compileSdk 33

    defaultConfig {

        manifestPlaceholders = [
                appAuthRedirectScheme: "eu.devhuba.anigafi"
        ]

        applicationId "eu.devhuba.anigafi"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField("String", "ANIME_CLIENT_ID", apikeyProperties['ANIME_CLIENT_ID'])
        buildConfigField("String", "ANIME_SECRET", apikeyProperties['ANIME_SECRET'])
        buildConfigField("String", "ANIME_ACCESS_TKN", apikeyProperties['ANIME_ACCESS_TKN'])

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

def compose_ui_version = "1.4.3"
def nav_version = "2.5.2"
def coil_version = "2.2.2"
def lifecycle_version = "2.5.1"
def retrofit_version = "2.9.0"
def hilt_version = "2.43.2"
def room_version = "2.4.2"

dependencies {
    //Basic
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // system bars customization
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.28.0"

    //Lazy logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    //Dagger
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    //Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    //Specific
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    implementation "io.github.oleksandrbalan:textflow:1.1.0"
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'net.openid:appauth:0.9.1'

//    Try animation on navigation - AnimatedNavHost
//    tutorial - https://shortcut.io/tech/adding-animations-to-your-jetpack-compose-navigation/
//    implementation "com.google.accompanist:accompanist-navigation-animation:0.16.0"

}